// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y–x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
    // Prepare 16-bit zero and one values for later
    Not16(in=x, out=notx);
    And16(a=x, b=notx, out=zero);
    Or16(a=x, b=notx, out=ones);

    // Prepare zx, nx, zy, ny in succession
    Mux16(a=x, b=zero, sel=zx, out=xzx);
    Mux16(a=y, b=zero, sel=zy, out=yzy);
    Not16(in=xzx, out=notxzx);
    Not16(in=yzy, out=notyzy);
    Mux16(a=xzx, b=notxzx, sel=nx, out=xnx); // Prepared x value
    Mux16(a=yzy, b=notyzy, sel=ny, out=yny); // Prepared y value

    // Factor in 'f' bit
    And16(a=xnx, b=yny, out=andxy);
    Add16(a=xnx, b=yny, out=plusxy);
    Mux16(a=andxy, b=plusxy, sel=f, out=fxy);

    // Factor in 'no' bit, and save useful bits of output including ng
    Not16(in=fxy, out=notfxy);
    Mux16(a=fxy, b=notfxy, sel=no, out=output, out[15]=ng, out[0..7]=out8, out[8..15]=out16);

    // Prepare zr from output
    Or8Way(in=out8, out=or8);
    Or8Way(in=out16, out=or16);
    Or(a=or8, b=or16, out=or);
    Not(in=or, out=zr);

    // Prepare output
    And16(a=output, b=output, out=out);
}
